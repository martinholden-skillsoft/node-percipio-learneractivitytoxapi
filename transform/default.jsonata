$.(
    $comment := '*******************************************************';
    $comment := '*******************************************************';
    $comment := 'Simple Example that converts Learner Activity Report';
    $comment := 'Records to xAPI Completed Statements';
    $comment := 'Author: martinholden-skillsoft';
    $comment := 'Date: 28-JUL-2021';
    $comment := 'Dependency: Uses functions from https://www.npmjs.com/package/jsonata-extended';
    $comment := '*******************************************************';
    $comment := '*******************************************************';

    $xapiduration := function($row) {(
        $duration := $number($row.duration ? $row.duration : 0);

        $hour := $floor($duration / 3600);
        $minute := $floor($duration / 60) - ($hour * 60);
        $second := $floor($duration % 60);

        $hourstr := $hour = 0 ? '' : $join([$hour&'','H']);
        $minutestr := $minute = 0 ? '' : $join([$minute&'','M']);
        $secondstr := $second = 0 ? '' : $join([$second&'','S']);

        $join(['PT',$hourstr,$minutestr,$secondstr],'');
    )};

    $getxapi := function ($contentType, $contentId) {(

        $assert($exists($contentType) and $length($contentType) != 0,'$getxapiid() - $contentType must be specified');
        $assert($exists($contentId) and $length($contentId) != 0,'$getxapiid() - $contentId must be specified');

        $contentTypeNormalized := $replace($lowercase($contentType), /[_-\s]/, '');
        $contentTypePathLabel := $contentTypeNormalized = 'linkedcontent' ? 'lc' : $lowercase($contentType);

        $PERCIPIO_NS := 'https://xapi.percipio.com/xapi';
        $EXPAPI_NS := 'http://adlnet.gov/expapi';
        $ADL_NS := 'https://w3id.org/xapi/adl';
        $TLA_NS := 'https://w3id.org/xapi/tla';
        $VIDEO_NS := 'https://w3id.org/xapi/video';

        $activityTypes := {
            'customchannel': $PERCIPIO_NS & '/activity-type/channel',
            'channel': $PERCIPIO_NS & '/activity-type/channel',
            'journey': $PERCIPIO_NS & '/activity-type/curriculum',
            'track': $PERCIPIO_NS & '/activity-type/curriculum',
            'course': $EXPAPI_NS & '/activities/course',
            'scheduledcontent': $EXPAPI_NS & '/activities/course',
            'linkedcontent': $EXPAPI_NS & '/activities/course',
            'video': $VIDEO_NS & '/activity-type/video',
            'book': $PERCIPIO_NS & '/activity-type/book',
            'audiobook': $PERCIPIO_NS & '/activity-type/audiobook',
            'assessment': $EXPAPI_NS & '/activities/assessment',
            'badge': $TLA_NS & '/activity-types/badge'
        };

        {
            'xapiActivityId': $join(['https://xapi.percipio.com/xapi', $contentTypePathLabel, $contentId],'/'),
            'xapiActivityTypeId': $join([$lookup($activityTypes, $contentTypeNormalized)],'/')
        }
    )};

    $xapi := (
        $.{
        "id": xapistatementid,
        "actor": {
            "objectType": "Agent",
            "account": {
                "homePage": $homepage,
                "name": $trim($lowercase(userId))
            }
        },
        "verb": {
            "id": "http://adlnet.gov/expapi/verbs/completed",
            "display": {
                "en": "completed"
            }
        },
        "object": xapiobject,
        "result": {
            "completion": true,
            "duration": $xapiduration($)
        },
        "timestamp": completedDate,
        "version": "1.0.0"
    }
    );

  $merge([$xapi]);
 )